ext {
    imVersion = System.getProperty("imVersion")
    bioVersion = System.getProperty("bioVersion")
}

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
        }
    }
    dependencies {
        classpath group: 'org.intermine', name: 'plugin', version: '1.+'
    }
}

subprojects {
    group = 'org.intermine'
    version = '2.0.0'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'dbmodel'
    apply plugin: 'biosource-dbmodel'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
        }
    }

    configurations {
        bioModel
        mergeModel
    }

    dependencies {
        compile files('/usr/local/projdata/0372/projects/MTG4/local/./git/jcvi-plant-genomics/intermine/bio/core/main/lib/biojava-1.7.1.jar')
        compile group: 'log4j', name: 'log4j', version: '1.2.17'
        compile group: 'org.intermine', name: 'bio-core', version: bioVersion
        compile group : "org.intermine", name: "intermine-resources", version: imVersion // log4j
        compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
        testCompile group: 'junit', name: 'junit', version: '4.8.2'
        testCompile group: "org.intermine", name: "intermine-integrate", version: imVersion, classifier: "testClasses"
        testCompile group: 'xmlunit', name: 'xmlunit', version: '1.0'
        mergeModel group : "org.intermine", name: "intermine-objectstore", version: imVersion
        bioModel group: 'org.intermine', name: 'bio-model', version: bioVersion, transitive: false
    }

    dbModelConfig {
        objectStoreName = "os.bio-test"
        modelName = "genomic"
        defaultInterminePropertiesFile = "default.intermine.bio-test.properties"
    }

    task copyMineProperties(type: Copy) {
        from "${System.env.HOME}/.intermine/intermine-bio-test.properties"
        into sourceSets.main.output.resourcesDir
        rename { fileName -> fileName.replace('intermine-bio-test.properties', 'intermine.properties') }
        inputs.sourceFiles.stopExecutionIfEmpty()
    }

    jar {
        exclude "intermine.properties"
    }

    tasks.withType(Test) {
        scanForTestClasses = false
        include '**/*Test.class'
    }

    test {
        ignoreFailures = true
        forkEvery = 1
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}
